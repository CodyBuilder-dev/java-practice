plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    aspectjtools
}

//tasks.withType(Test) {
//    systemProperty "file.encoding", "UTF-8"
//}
tasks.withType(JavaExec).configureEach {
    jvmArgs = ['-Dfile.encoding=UTF-8', '-Dstdout.encoding=UTF-8', '-Dstderr.encoding=UTF-8',
               '--add-opens', 'java.base/java.lang=ALL-UNNAMED']
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.25'

    implementation 'cglib:cglib:3.3.0'

    implementation 'org.aspectj:aspectjrt:1.9.25.1'
    aspectjtools 'org.aspectj:aspectjtools:1.9.25.1'

    implementation 'net.openhft:affinity:3.27ea1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
}

tasks.register('compileAjc') {
    group = 'build'
    description = 'Compile with AspectJ (compile-time weaving)'
    dependsOn 'processResources'

    doLast {
        ant.taskdef(
                resource: 'org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties',
                classpath: configurations.aspectjtools.asPath
        )

        ant.iajc(
                source: '17',
                target: '17',
                destDir: sourceSets.main.java.destinationDirectory.get().asFile,
                classpath: sourceSets.main.compileClasspath.asPath,
                showWeaveInfo: 'true'
        ) {
            sourceroots {
                sourceSets.main.java.srcDirs.each { dir ->
                    pathelement(location: dir)
                }
            }
        }
    }
}

compileJava.enabled = false
classes.dependsOn 'compileAjc'